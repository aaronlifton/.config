# usage:
#   fish -c 'spinner' &
#   set pid $last_pid
#   sleep 2
#   kill $pid
#   echo -ne '\r'
function spinner -d 'A simple loading spinner for Fish'
    argparse c/clock d/dot m/moon w/weather -- $argv 2>/dev/null

    set -l cr '\r'
    set -l spaces ' '
    set -l chars ''
    set -l template '%b%b%s'

    # https://github.com/sindresorhus/cli-spinners/blob/main/spinners.json
    set -l plain_chars '|' / - '\\'
    set -l dot_chars 'â ‹' 'â ™' 'â ¹' 'â ¸' 'â ¼' 'â ´' 'â ¦' 'â §' 'â ‡' 'â '

    # these are full-width characters so they only require 1 space for alignment
    set -l clock_chars '\U1F55B' # ğŸ•› twelve oâ€™clock
    set -a clock_chars '\U1F55C' # ğŸ•œ one-thirty
    set -a clock_chars '\U1F552' # ğŸ•’ three oâ€™clock
    set -a clock_chars '\U1F55F' # ğŸ•Ÿ four-thirty
    set -a clock_chars '\U1F555' # ğŸ•• six oâ€™clock
    set -a clock_chars '\U1F562' # ğŸ•¢ seven-thirty
    set -a clock_chars '\U1F558' # ğŸ•˜ nine oâ€™clock
    set -a clock_chars '\U1F565' # ğŸ•¥ ten-thirty

    set -l moon_chars '\U1F311' # ğŸŒ‘ new moon
    set -a moon_chars '\U1F312' # ğŸŒ’ waxing crescent moon
    set -a moon_chars '\U1F313' # ğŸŒ“ first quarter moon
    set -a moon_chars '\U1F314' # ğŸŒ” waxing gibbous moon
    set -a moon_chars '\U1F315' # ğŸŒ• full moon
    set -a moon_chars '\U1F316' # ğŸŒ– waning gibbous moon
    set -a moon_chars '\U1F317' # ğŸŒ— last quarter moon
    set -a moon_chars '\U1F318' # ğŸŒ˜ waning crescent moon

    # these use a "variation selector" to force them to be rendered as images
    # note: you can run into issues with these
    set -l weather_chars '\U1F324\UFE0F' # ğŸŒ¤ï¸ sun behind small cloud
    set -a weather_chars '\U1F326\UFE0F' # ğŸŒ¦ï¸ sun behind rain cloud
    set -a weather_chars '\U1F327\UFE0F' # ğŸŒ§ï¸ cloud with rain
    set -a weather_chars '\U26C8\UFE0F' # â›ˆï¸ cloud with lightning and rain
    set -a weather_chars '\U1F329\UFE0F' # ğŸŒ©ï¸ cloud with lightning
    set -a weather_chars '\U1F325\UFE0F' # ğŸŒ¥ï¸ sun behind large cloud

    if set -q _flag_c
        set chars $clock_chars
    else if set -q _flag_m
        set chars $moon_chars
    else if set -q _flag_w
        set chars $weather_chars
        set spaces '  '
    else if set -q _flag_d
        set chars $dot_chars
        set template '%b%s%s'
    else
        set chars $plain_chars
        set template '%b%s%s'
    end

    # I cannot get something like `trap 'echo -ne "\r"' EXIT INT` to work
    # so you just have to clear it manually with `echo -ne '\r'`
    set -l i 1
    while true
        printf $template $cr $chars[$i] $spaces
        set i (math $i % (count $chars) + 1)
        sleep 0.15
    end
end
